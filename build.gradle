buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE")
    }
}
plugins {
    id "com.commercehub.cucumber-jvm" version "0.13"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

addCucumberSuite 'cucumberTest'

cucumber {
    maxParallelForks = 3
}

group 'com.devopsicon.microservices'
version '1.0-SNAPSHOT'
sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    perfTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    perfTestCompile.extendsFrom testCompile
    perfTestRuntime.extendsFrom testRuntime
}

task perfTest(type:Test){
    description = "Run performance tests (located in src/perfTest/...)."
    testClassesDir = sourceSets.perfTest.output.classesDir
    classpath = sourceSets.perfTest.runtimeClasspath
}

jar {
    baseName = 'sales-microservice'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compileOnly 'org.projectlombok:lombok:1.16.20'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.8.0'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.8.0'

    testCompile("org.springframework.boot:spring-boot-starter-test")

    cucumberTestCompile group: 'io.cucumber', name: 'cucumber-java', version: '2.3.1'

    compile group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.0.7'

    // To use JUnit assertions in the step definitions:
    cucumberTestCompile 'junit:junit:4.12'
    cucumberTestCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'

    perfTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.0'
    perfTestCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.1.0'
    perfTestCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.1.0'

    perfTestCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}